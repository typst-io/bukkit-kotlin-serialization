plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
    id 'io.typst.spigradle' version '3.0.4'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.2.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'signing'
}

group = 'io.typst'
version = '3.1.2'

repositories {
    mavenCentral()
    spigotmc()
}

dependencies {
    compileOnly spigot('1.16.5')
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'
    implementation 'com.charleskorn.kaml:kaml-jvm:0.96.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(11)
}

spigot {
    apiVersion '1.16'
}

assemble.dependsOn(shadowJar)

publishing {
    publications {
        maven(MavenPublication) {
            groupId = rootProject.group
            artifactId = rootProject.name
            version = rootProject.version
            from components.java
            pom {
                name.set("${project.group}:${project.name}")
                description.set("Static object-json mapping for Bukkit.")
                url.set("https://github.com/typst-io/bukkit-kotlin-serialization")
                licenses {
                    license {
                        name.set("GNU Lesser General Public License v3")
                        url.set("https://www.gnu.org/licenses/lgpl-3.0.txt")
                    }
                }
                developers {
                    developer {
                        id.set("entrypointkr")
                        name.set("Junhyung Im")
                        email.set("entrypointkr@gmail.com")
                    }
                }
                scm {
                    connection.set("scm:git:git://github.com/typst-io/bukkit-kotlin-serialization.git")
                    developerConnection.set("scm:git:ssh://github.com:typst-io/bukkit-kotlin-serialization.git")
                    url.set("https://github.com/typst-io/bukkit-kotlin-serialization/tree/master")
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatypeReleases"
            url = uri("https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")
            credentials {
                username = findProperty("ossrhUsername")?.toString()
                password = findProperty("ossrhPassword")?.toString()
            }
        }
    }
}

signing {
    sign(publishing.publications["maven"])
}

tasks.register("publishCentralPortal") {
    group = 'publishing'
    doLast {
        def url = new URL("https://ossrh-staging-api.central.sonatype.com/manual/upload/defaultRepository/$project.group")
        println(url)
        def con = url.openConnection() as HttpURLConnection
        def username = findProperty("ossrhUsername")?.toString()
        def password = findProperty("ossrhPassword")?.toString()
        def credential = Base64.encoder.encodeToString("$username:$password".getBytes())
        def authValue = "Bearer $credential"
        con.setRequestMethod("POST")
        con.setRequestProperty("Authorization", authValue)
        println(con.responseCode)
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}
